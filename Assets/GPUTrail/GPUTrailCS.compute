#pragma kernel InitHeader
#pragma kernel InitNode
#pragma kernel EmitTrail

#pragma kernel UpdateSourceBuffer
#pragma kernel UpdateFromSourceBuffer
#pragma kernel AppendDeadToNodePool

#define TRAIL_BLOCK_SIZE 256

// #define SourceType Particle
#define SourceType float3

#include "GPUTrailData.cginc"

StructuredBuffer<SourceType> _ParticleBuffer;
RWStructuredBuffer<SourceType> _SourceBuffer;

RWStructuredBuffer<TrailHeader> _TrailHeaderBuffer;
int _TrailHeaderBufferCount;
AppendStructuredBuffer<int> _TrailHeaderIndexBufferAppend;
ConsumeStructuredBuffer<int> _TrailHeaderIndexBufferConsume;

RWStructuredBuffer<TrailNode> _TrailNodeBuffer;
int _TrailNodeBufferCount;
AppendStructuredBuffer<int> _TrailNodeIndexBufferAppend;
ConsumeStructuredBuffer<int> _TrailNodeIndexBufferConsume;
AppendStructuredBuffer<int> _TrailNodeIndexDeadBufferAppend;
ConsumeStructuredBuffer<int> _TrailNodeIndexDeadBufferConsume;

int _EmitTrailNum;
int _EmitTrailLen;

TrailHeader InitHeader(int idx, TrailHeader header)
{
    header = (TrailHeader)0;
    header.state = TS_READY;
    header.first = -1;
    header.currentlength = -1;
    header.maxLength = -1;
    return header;
}
TrailNode InitNode(int idx, TrailNode node)
{
    node = (TrailNode)0;
    node.prev = node.next = -1;
    node.idx = idx;
    node.uvy = 0;
    node.pos = 0;
    return node;
}


[numthreads(TRAIL_BLOCK_SIZE,1,1)]
void InitHeader (uint3 id : SV_DispatchThreadID)
{
    const int hid = id.x;
    if(hid >= _TrailHeaderBufferCount) return;

    _TrailHeaderBuffer[hid] = InitHeader(hid, _TrailHeaderBuffer[hid]);
    _TrailHeaderIndexBufferAppend.Append(hid);
}
 
[numthreads(TRAIL_BLOCK_SIZE,1,1)]
void InitNode (uint3 id : SV_DispatchThreadID)
{
    const int nid = id.x;
    if(nid >= _TrailNodeBufferCount) return;

    _TrailNodeBuffer[nid] = InitNode(nid, _TrailNodeBuffer[nid]);
    _TrailNodeIndexBufferAppend.Append(nid);
}

[numthreads(TRAIL_BLOCK_SIZE,1,1)]
void EmitTrail (uint3 id : SV_DispatchThreadID)
{
    const int tid = id.x;
    if(tid >= _EmitTrailNum) return;

    const int hid = _TrailHeaderIndexBufferConsume.Consume();
    const int maxLength = _EmitTrailLen;

    TrailHeader header = _TrailHeaderBuffer[hid];
    header = InitHeader(hid, header);
    header.state = TS_ACTIVE;
    header.currentlength = 0;
    header.maxLength = maxLength;
    _TrailHeaderBuffer[hid] = header;
}


[numthreads(TRAIL_BLOCK_SIZE,1,1)]
void UpdateSourceBuffer (uint3 id : SV_DispatchThreadID)
{
    const int pid = id.x;
    _SourceBuffer[pid] = _ParticleBuffer[pid];
}

void UpdateNode(inout TrailNode node, in SourceType p)
{
    node.pos = p;
}

[numthreads(TRAIL_BLOCK_SIZE,1,1)]
void UpdateFromSourceBuffer (uint3 id : SV_DispatchThreadID)
{
    const int hid = id.x;
    TrailHeader header = _TrailHeaderBuffer[hid];
    if(!IsActive(header)) return;

    const int firstid = header.first;
    const int maxLength = header.maxLength;
    const int currentlength = header.currentlength;

    const int pid = hid;
    SourceType p = _SourceBuffer[pid];

    //consume a new node anyway
    int nid = _TrailNodeIndexBufferConsume.Consume();
    TrailNode node = _TrailNodeBuffer[nid];
    node = InitNode(nid, node);
    UpdateNode(node, p);

    //add new node before first node
    if(firstid != -1)
    {
        TrailNode first = _TrailNodeBuffer[firstid];
        first.prev = node.idx;
        node.next = first.idx;
        _TrailNodeBuffer[firstid] = first;
    }
    _TrailNodeBuffer[nid] = node;

    header.first = nid;

    //update local idx for each node
    int localIdx = 0;
    int last = -1;
    int curr = nid;
    while(curr != -1)
    {
        TrailNode node = _TrailNodeBuffer[curr];
        node.uvy = 1-(currentlength > 1?(localIdx * 1.0f)/(currentlength-1):0);
        _TrailNodeBuffer[curr] = node;

        localIdx++;
        last = curr;
        curr = node.next;
    }

    //remove last new if it reaches max length
    if(currentlength == maxLength)
    {
        int prev = _TrailNodeBuffer[last].prev;
        _TrailNodeBuffer[prev].next = -1;
        _TrailNodeIndexDeadBufferAppend.Append(last);
    }

    header.currentlength = min(currentlength+1, maxLength);
    _TrailHeaderBuffer[hid] = header;
}

[numthreads(1,1,1)]
void AppendDeadToNodePool (uint3 id : SV_DispatchThreadID)
{
    const int nid = _TrailNodeIndexDeadBufferConsume.Consume();
    _TrailNodeBuffer[nid] = InitNode(nid, _TrailNodeBuffer[nid]);
    _TrailNodeIndexBufferAppend.Append(nid);
}
